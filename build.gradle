plugins {
    id 'java'
    id "org.sonarqube" version "2.6.2"
    id "org.liquibase.gradle" version "2.0.1"
    id 'jacoco'
    id 'war'
}
apply from: 'https://raw.github.com/gretty-gradle-plugin/gretty/master/pluginScripts/gretty.plugin'

group 'ru.innopolis.stc12'
version '1.0-SNAPSHOT'

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectName", "bookSharingProject"
        property "sonar.projectKey", "bookSharing"
        property "sonar.coverage.exclusions", "**/src/main/java/ru/innopolis/stc12/booksharing/model/pojo/*,**/src/main/java/ru/innopolis/stc12/booksharing/model/dao/entity/*"
        property "sonar.organization", "stc12team"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.language", "java"
        property "sonar.branch.name", "develop"
        property "sonar.login", System.getenv("SONARCLOUD_TOKEN")
        property "sonar.jacoco.reportPaths", "$rootProject.buildDir/jacoco/alltest.exec"
    }
}
sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.2.0-M1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.22.0'
    compile 'log4j:log4j:1.2.17'
    compile group: 'org.springframework', name: 'spring-webmvc', version: '4.3.20.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-web', version: '4.2.9.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-taglibs', version: '4.2.9.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-config', version: '4.2.9.RELEASE'
    compile group: 'jstl', name: 'jstl', version: '1.2'
    compile group: 'org.springframework', name: 'spring-jdbc', version: '4.3.20.RELEASE'
    compile group: 'org.springframework', name: 'spring-orm', version: '4.3.12.RELEASE'
    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.12.Final'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.2'
    compile group: 'com.cloudinary', name: 'cloudinary-core', version: '1.21.0'
    compile group: 'com.cloudinary', name: 'cloudinary-http44', version: '1.21.0'
    compile group: 'com.cloudinary', name: 'cloudinary-taglib', version: '1.21.0'
    compile group: 'commons-fileupload', name: 'commons-fileupload', version: '1.3.3'
    compile 'com.github.jsimone:webapp-runner:8.5.11.3'
    compile 'com.google.guava:guava:26.0-jre'
    liquibaseRuntime 'org.liquibase:liquibase-core:3.6.2'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.0.1'
    liquibaseRuntime 'org.postgresql:postgresql:42.2.5'
    compile group: 'com.vladmihalcea', name: 'hibernate-types-52', version: '2.3.4' //PostgreSQLEnumType
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
tasks.withType(Test) {
    jacoco {
        destinationFile = file("$rootProject.buildDir/jacoco/alltest.exec")
    }
}

liquibase {
    activities {
        main {
            changeLogFile 'src/main/db/changelog.xml'
            url project.properties['url'] ?: System.getenv("HEROKU_DATABASE_URL") ?: System.getenv("DATABASE_URL")
            username project.properties['username'] ?: System.getenv("DATABASE_USERNAME")
            password project.properties['password'] ?: System.getenv("DATABASE_PASSWORD")
        }
    }
}

task stage() {
    dependsOn clean, war
}
war.mustRunAfter clean

task copyToLib(type: Copy) {
    into "$buildDir/server"
    from(configurations.compile) {
        include "webapp-runner*"
    }
}

stage.dependsOn(copyToLib)
